const std = @import("std");

const http = @import("http.zig");
const RequestParser = @import("parser.zig").RequestParser;
const RequestBodyReader = @import("parser.zig").RequestBodyReader;
const ParseError = @import("parser.zig").ParseError;
const ServerConfig = @import("config.zig").ServerConfig;
const Response = @import("response.zig").Response;
pub const Cookie = @import("cookie.zig").Cookie;
pub const SessionData = @import("middleware/Session.zig").SessionData;

pub const Request = struct {
    method: http.Method = undefined,
    url: []const u8 = "",
    version_major: u8 = 0,
    version_minor: u8 = 0,
    headers: http.Headers = .{},
    content_type: ?http.ContentType = null,
    params: std.StringHashMapUnmanaged([]const u8) = .{},
    query: std.StringHashMapUnmanaged([]const u8) = .{},

    arena: std.mem.Allocator,
    io: std.Io = undefined,

    // Body reading support
    parser: *RequestParser,
    conn: *std.Io.Reader,
    body_reader_buffer: [1024]u8 = undefined,
    config: ServerConfig.Request = .{},
    _body: ?[]const u8 = null,
    _body_read: bool = false,
    _fd: std.StringHashMapUnmanaged([]const u8) = .{},
    _fd_read: bool = false,
    _mfd: std.StringHashMapUnmanaged(MultipartForm.Entry) = .{},
    _mfd_read: bool = false,
    session: SessionData = .{},

    // 100-continue support
    response: ?*Response = null,
    expects_continue: bool = false,

    pub fn reset(self: *Request) void {
        const arena = self.arena;
        const io = self.io;
        const parser = self.parser;
        const conn = self.conn;
        const cfg = self.config;
        const res = self.response;
        self.* = .{
            .arena = arena,
            .io = io,
            .parser = parser,
            .conn = conn,
            .config = cfg,
            .response = res,
        };
    }

    pub fn reader(self: *Request) RequestBodyReader {
        // If body has already been read, return a reader for the cached body
        if (self._body_read) {
            const cached_body = self._body orelse &.{};
            var r = RequestBodyReader.init(self.parser, self.conn, &self.body_reader_buffer);
            r.interface = std.Io.Reader.fixed(cached_body);
            return r;
        }

        // Return the streaming body reader
        var r = RequestBodyReader.init(self.parser, self.conn, &self.body_reader_buffer);
        r.request = self;
        return r;
    }

    /// Read the entire body into memory. Result is cached for subsequent calls.
    pub fn body(self: *Request) !?[]const u8 {
        if (self._body_read) {
            return self._body;
        }

        var r = self.reader();
        const result = r.interface.allocRemaining(self.arena, .limited(self.config.max_body_size)) catch |err| switch (err) {
            error.StreamTooLong => return error.BodyTooBig,
            else => return err,
        };

        self._body_read = true;
        if (result.len == 0) {
            self._body = null;
            return null;
        }
        self._body = result;
        return result;
    }

    /// Parse body as JSON into type T
    pub fn json(self: *Request, comptime T: type) !?T {
        const b = try self.body() orelse return null;
        return try std.json.parseFromSliceLeaky(T, self.arena, b, .{});
    }

    /// Parse body as a generic JSON value
    pub fn jsonValue(self: *Request) !?std.json.Value {
        const b = try self.body() orelse return null;
        return try std.json.parseFromSliceLeaky(std.json.Value, self.arena, b, .{});
    }

    /// Parse body as a JSON object
    pub fn jsonObject(self: *Request) !?std.json.ObjectMap {
        const value = try self.jsonValue() orelse return null;
        switch (value) {
            .object => |o| return o,
            else => return null,
        }
    }

    /// Get cookies from the request
    pub fn cookies(self: *const Request) Cookie {
        return .{
            .header = self.headers.get("Cookie") orelse "",
        };
    }

    /// Parse the body as a form (application/x-www-form-urlencoded)
    pub fn formData(self: *Request) !*std.StringHashMapUnmanaged([]const u8) {
        if (self._fd_read) {
            return &self._fd;
        }

        if (self.content_type == null or self.content_type != .form) {
            return error.NotForm;
        }

        const buffer = try self.body() orelse {
            self._fd_read = true;

            return &self._fd;
        };

        var entry_iterator = std.mem.splitScalar(u8, buffer, '&');

        while (entry_iterator.next()) |entry| {
            if (self._fd.count() >= self.config.max_form_count) {
                return error.TooManyFormFields;
            }

            if (std.mem.indexOfScalar(u8, entry, '=')) |separator| {
                const key = try Request.urlUnescape(self.arena, entry[0..separator]);
                const value = try Request.urlUnescape(self.arena, entry[separator + 1 ..]);

                try self._fd.put(self.arena, key, value);
            } else {
                try self._fd.put(self.arena, try Request.urlUnescape(self.arena, entry), "");
            }
        }

        self._fd_read = true;

        return &self._fd;
    }

    /// Parse the body as a multipart form (multipart/form-data)
    pub fn multiFormData(self: *Request) !*std.StringHashMapUnmanaged(MultipartForm.Entry) {
        if (self._mfd_read) {
            return &self._mfd;
        }

        if (self.content_type == null or self.content_type != .multipart_form) {
            return error.NotMultipartForm;
        }

        const buffer = try self.body() orelse {
            self._mfd_read = true;

            return &self._mfd;
        };

        // The following chunk of code is from https://github.com/karlseguin/http.zig, see LICENSE for more details.

        var boundary_buf: [72]u8 = undefined;
        const boundary = blk: {
            const directive = (self.headers.get("Content-Type") orelse unreachable)["multipart/form-data".len..];
            for (directive, 0..) |b, i| loop: {
                if (b != ' ' and b != ';') {
                    if (std.ascii.startsWithIgnoreCase(directive[i..], "boundary=")) {
                        const raw_boundary = directive["boundary=".len + i ..];
                        if (raw_boundary.len > 0 and raw_boundary.len <= 70) {
                            boundary_buf[0] = '-';
                            boundary_buf[1] = '-';
                            if (raw_boundary[0] == '"') {
                                if (raw_boundary.len > 2 and raw_boundary[raw_boundary.len - 1] == '"') {
                                    // it's really -2, since we need to strip out the two quotes
                                    // but buf is already at + 2, so they cancel out.
                                    const end = raw_boundary.len;
                                    @memcpy(boundary_buf[2..end], raw_boundary[1 .. raw_boundary.len - 1]);
                                    break :blk boundary_buf[0..end];
                                }
                            } else {
                                const end = 2 + raw_boundary.len;
                                @memcpy(boundary_buf[2..end], raw_boundary);
                                break :blk boundary_buf[0..end];
                            }
                        }
                    }
                    // not valid, break out of the loop so we can return
                    // an error.InvalidMultiPartFormDataHeader
                    break :loop;
                }
            }
            return error.InvalidMultiPartFormDataHeader;
        };

        var entry_it = std.mem.splitSequence(u8, buffer, boundary);

        {
            // We expect the body to begin with a boundary
            const first = entry_it.next() orelse {
                self._mfd_read = true;
                return &self._mfd;
            };
            if (first.len != 0) {
                return error.InvalidMultiPartEncoding;
            }
        }

        while (entry_it.next()) |entry| {
            // body ends with -- after a final boundary
            if (entry.len == 4 and entry[0] == '-' and entry[1] == '-' and entry[2] == '\r' and entry[3] == '\n') {
                break;
            }

            if (self._mfd.count() >= self.config.max_multiform_count) {
                return error.TooManyMultiFormFields;
            }

            if (entry.len < 2 or entry[0] != '\r' or entry[1] != '\n') return error.InvalidMultiPartEncoding;

            // [2..] to skip our boundary's trailing line terminator
            const field = try MultipartForm.parseMultiPartEntry(entry[2..]);
            try self._mfd.put(self.arena, field.name, field.value);
        }

        // End of chunk.

        self._mfd_read = true;

        return &self._mfd;
    }

    /// Unescape a URL-encoded string
    /// Converts %XX hex sequences to bytes and + to space
    pub fn urlUnescape(allocator: std.mem.Allocator, input: []const u8) ![]const u8 {
        var has_plus = false;
        var unescaped_len = input.len;

        var in_i: usize = 0;
        while (in_i < input.len) {
            const b = input[in_i];
            if (b == '%') {
                if (in_i + 2 >= input.len or !std.ascii.isHex(input[in_i + 1]) or !std.ascii.isHex(input[in_i + 2])) {
                    return error.InvalidEscapeSequence;
                }
                in_i += 3;
                unescaped_len -= 2;
            } else if (b == '+') {
                has_plus = true;
                in_i += 1;
            } else {
                in_i += 1;
            }
        }

        // no encoding, and no plus. nothing to unescape
        if (unescaped_len == input.len and !has_plus) {
            return input;
        }

        const out = try allocator.alloc(u8, unescaped_len);

        in_i = 0;
        for (0..unescaped_len) |i| {
            const b = input[in_i];
            if (b == '%') {
                out[i] = decodeHex(input[in_i + 1]) << 4 | decodeHex(input[in_i + 2]);
                in_i += 3;
            } else if (b == '+') {
                out[i] = ' ';
                in_i += 1;
            } else {
                out[i] = b;
                in_i += 1;
            }
        }

        return out;
    }

    fn decodeHex(c: u8) u8 {
        return switch (c) {
            '0'...'9' => c - '0',
            'A'...'F' => c - 'A' + 10,
            'a'...'f' => c - 'a' + 10,
            else => 0,
        };
    }
};

const MultipartForm = struct {
    const Entry = struct { value: []const u8, filename: ?[]const u8 = null };

    // The following chunk of code is from https://github.com/karlseguin/http.zig, see LICENSE for more details.

    const Field = struct {
        name: []const u8,
        value: Entry,
    };

    fn parseMultiPartEntry(entry: []const u8) !Field {
        var pos: usize = 0;
        var attributes: ?ContentDispositionAttributes = null;

        while (true) {
            const end_line_pos = std.mem.indexOfScalarPos(u8, entry, pos, '\n') orelse return error.InvalidMultiPartEncoding;
            const line = entry[pos..end_line_pos];

            pos = end_line_pos + 1;
            if (line.len == 0 or line[line.len - 1] != '\r') return error.InvalidMultiPartEncoding;

            if (line.len == 1) {
                break;
            }

            // we need to look for the name
            if (std.ascii.startsWithIgnoreCase(line, "content-disposition:") == false) {
                continue;
            }

            const value = trimLeadingSpace(line["content-disposition:".len..]);
            if (std.ascii.startsWithIgnoreCase(value, "form-data;") == false) {
                return error.InvalidMultiPartEncoding;
            }

            // constCast is safe here because we know this ultimately comes from one of our buffers
            const value_start = "form-data;".len;
            const value_end = value.len - 1; // remove the trailing \r
            attributes = try getContentDispositionAttributes(@constCast(trimLeadingSpace(value[value_start..value_end])));
        }

        const value = entry[pos..];
        if (value.len < 2 or value[value.len - 2] != '\r' or value[value.len - 1] != '\n') {
            return error.InvalidMultiPartEncoding;
        }

        const attr = attributes orelse return error.InvalidMultiPartEncoding;

        return .{
            .name = attr.name,
            .value = .{
                .value = value[0 .. value.len - 2],
                .filename = attr.filename,
            },
        };
    }

    const ContentDispositionAttributes = struct {
        name: []const u8,
        filename: ?[]const u8 = null,
    };

    fn getContentDispositionAttributes(fields: []u8) !ContentDispositionAttributes {
        var pos: usize = 0;

        var name: ?[]const u8 = null;
        var filename: ?[]const u8 = null;

        while (pos < fields.len) {
            {
                const b = fields[pos];
                if (b == ';' or b == ' ' or b == '\t') {
                    pos += 1;
                    continue;
                }
            }

            const sep = std.mem.indexOfScalarPos(u8, fields, pos, '=') orelse return error.InvalidMultiPartEncoding;
            const field_name = fields[pos..sep];

            // skip the equal
            const value_start = sep + 1;
            if (value_start == fields.len) {
                return error.InvalidMultiPartEncoding;
            }

            var value: []const u8 = undefined;
            if (fields[value_start] != '"') {
                // Search from value_start, not pos: a stray ';' inside the
                // field name (malformed input the parser doesn't reject)
                // would otherwise place value_end before value_start.
                const value_end = std.mem.indexOfScalarPos(u8, fields, value_start, ';') orelse fields.len;
                pos = value_end;
                value = fields[value_start..value_end];
            } else blk: {
                // skip the double quote
                pos = value_start + 1;
                var write_pos = pos;
                while (pos < fields.len) {
                    switch (fields[pos]) {
                        '\\' => {
                            // Trailing backslash with no character to escape.
                            if (pos + 1 >= fields.len) {
                                return error.InvalidMultiPartEncoding;
                            }
                            // supposedly MSIE doesn't always escape \, so if the \ isn't escape
                            // one of the special characters, it must be a single \. This is what Go does.
                            switch (fields[pos + 1]) {
                                // from Go's mime parser func isTSpecial(r rune) bool
                                '(', ')', '<', '>', '@', ',', ';', ':', '"', '/', '[', ']', '?', '=' => |n| {
                                    fields[write_pos] = n;
                                    pos += 1;
                                },
                                else => fields[write_pos] = '\\',
                            }
                        },
                        '"' => {
                            pos += 1;
                            value = fields[value_start + 1 .. write_pos];
                            break :blk;
                        },
                        else => |b| fields[write_pos] = b,
                    }
                    pos += 1;
                    write_pos += 1;
                }
                return error.InvalidMultiPartEncoding;
            }

            if (std.mem.eql(u8, field_name, "name")) {
                name = value;
            } else if (std.mem.eql(u8, field_name, "filename")) {
                filename = value;
            }
        }

        return .{
            .name = name orelse return error.InvalidMultiPartEncoding,
            .filename = filename,
        };
    }

    inline fn trimLeadingSpaceCount(in: []const u8) struct { []const u8, usize } {
        if (in.len > 1 and in[0] == ' ') {
            // very common case
            const n = in[1];
            if (n != ' ' and n != '\t') {
                return .{ in[1..], 1 };
            }
        }

        for (in, 0..) |b, i| {
            if (b != ' ' and b != '\t') return .{ in[i..], i };
        }
        return .{ "", in.len };
    }

    inline fn trimLeadingSpace(in: []const u8) []const u8 {
        const out, _ = trimLeadingSpaceCount(in);
        return out;
    }

    // End of chunk.
};

const ParseHeadersError = std.Io.Reader.Error || ParseError || error{ IncompleteRequest, OutOfMemory };

/// Parse HTTP headers from a reader and prepare for body reading.
/// Returns error.EndOfStream if connection closed cleanly with no data.
/// Returns error.IncompleteRequest if connection closed mid-request.
pub fn parseHeaders(reader: *std.Io.Reader, parser: *RequestParser) ParseHeadersError!void {
    // Re-pre-allocate headers each call to handle keep-alive (arena was reset).
    parser.request.headers = try http.Headers.init(parser.request.arena, parser.request.config.max_header_count);
    var parsed_len: usize = 0;
    while (!parser.state.headers_complete) {
        const buffered = reader.buffered();
        const unparsed = buffered[parsed_len..];
        if (unparsed.len > 0) {
            parser.feed(unparsed) catch |err| switch (err) {
                error.Paused => {
                    const consumed = parser.getConsumedBytes(unparsed.ptr);
                    parsed_len += consumed;
                    continue;
                },
                else => |e| return e,
            };
            parsed_len += unparsed.len;
            continue;
        }
        reader.fillMore() catch |err| switch (err) {
            error.EndOfStream => {
                if (parsed_len == 0) return error.EndOfStream;
                return error.IncompleteRequest;
            },
            else => |e| return e,
        };
    }
    reader.toss(parsed_len);
    parser.resumeParsing();

    // Shorten buffer so body reading doesn't overwrite header data.
    // Headers remain valid in buffer[0..headers_len], body uses the rest.
    std.debug.assert(reader.seek == parsed_len);
    const headers_len = reader.seek;
    reader.buffer = reader.buffer[headers_len..];
    reader.end -= headers_len;
    reader.seek = 0;

    // Feed empty buffer to advance state machine for bodyless requests
    parser.feed(&.{}) catch |err| switch (err) {
        error.Paused => {},
        else => |e| return e,
    };
}

test "Request.body: basic POST" {
    var arena = std.heap.ArenaAllocator.init(std.testing.allocator);
    defer arena.deinit();

    const raw_request = "POST /test HTTP/1.1\r\nContent-Length: 5\r\n\r\nhello";
    var reader = std.Io.Reader.fixed(raw_request);

    var req: Request = .{
        .arena = arena.allocator(),
        .conn = &reader,
        .parser = undefined,
    };

    var parser: RequestParser = undefined;
    try parser.init(&req);
    defer parser.deinit();
    req.parser = &parser;

    try parseHeaders(&reader, &parser);

    const body = try req.body();
    try std.testing.expectEqualStrings("hello", body.?);
}

test "Request.body: large body over 128 bytes" {
    var arena = std.heap.ArenaAllocator.init(std.testing.allocator);
    defer arena.deinit();

    const body_content = "A"**256;
    const raw_request = "POST /test HTTP/1.1\r\nContent-Length: 256\r\n\r\n" ++ body_content;
    var reader = std.Io.Reader.fixed(raw_request);

    var req: Request = .{
        .arena = arena.allocator(),
        .conn = &reader,
        .parser = undefined,
    };

    var parser: RequestParser = undefined;
    try parser.init(&req);
    defer parser.deinit();
    req.parser = &parser;

    try parseHeaders(&reader, &parser);

    const body = try req.body();
    try std.testing.expectEqual(256, body.?.len);
    try std.testing.expectEqualStrings(body_content, body.?);
}

test "Request.cookies: parse cookies from header" {
    var arena = std.heap.ArenaAllocator.init(std.testing.allocator);
    defer arena.deinit();

    const raw_request = "GET /test HTTP/1.1\r\nCookie: session=abc123; user=john\r\n\r\n";
    var reader = std.Io.Reader.fixed(raw_request);

    var req: Request = .{
        .arena = arena.allocator(),
        .conn = &reader,
        .parser = undefined,
    };

    var parser: RequestParser = undefined;
    try parser.init(&req);
    defer parser.deinit();
    req.parser = &parser;

    try parseHeaders(&reader, &parser);

    const cookies = req.cookies();
    try std.testing.expectEqualStrings("abc123", cookies.get("session").?);
    try std.testing.expectEqualStrings("john", cookies.get("user").?);
    try std.testing.expectEqual(null, cookies.get("missing"));
}

test "Request.formData: basic key and value" {
    var arena = std.heap.ArenaAllocator.init(std.testing.allocator);
    defer arena.deinit();

    const raw_request = "POST /test HTTP/1.1\r\nContent-Type: application/x-www-form-urlencoded\r\nContent-Length: 15\r\n\r\nfoo=123&bar=abc";
    var reader = std.Io.Reader.fixed(raw_request);

    var req: Request = .{
        .arena = arena.allocator(),
        .conn = &reader,
        .parser = undefined,
    };

    var parser: RequestParser = undefined;
    try parser.init(&req);
    defer parser.deinit();
    req.parser = &parser;

    try parseHeaders(&reader, &parser);

    const form_data = try req.formData();
    try std.testing.expectEqualStrings("123", form_data.get("foo").?);
    try std.testing.expectEqualStrings("abc", form_data.get("bar").?);
}

test "Request.formData: URL-encoded key and value" {
    var arena = std.heap.ArenaAllocator.init(std.testing.allocator);
    defer arena.deinit();

    const raw_request = "POST /test HTTP/1.1\r\nContent-Type: application/x-www-form-urlencoded\r\nContent-Length: 17\r\n\r\nfoo+bar=123%21abc";
    var reader = std.Io.Reader.fixed(raw_request);

    var req: Request = .{
        .arena = arena.allocator(),
        .conn = &reader,
        .parser = undefined,
    };

    var parser: RequestParser = undefined;
    try parser.init(&req);
    defer parser.deinit();
    req.parser = &parser;

    try parseHeaders(&reader, &parser);

    const form_data = try req.formData();
    try std.testing.expectEqualStrings("123!abc", form_data.get("foo bar").?);
}

test "Request.formData: entry with no value" {
    var arena = std.heap.ArenaAllocator.init(std.testing.allocator);
    defer arena.deinit();

    const raw_request = "POST /test HTTP/1.1\r\nContent-Type: application/x-www-form-urlencoded\r\nContent-Length: 3\r\n\r\nfoo";
    var reader = std.Io.Reader.fixed(raw_request);

    var req: Request = .{
        .arena = arena.allocator(),
        .conn = &reader,
        .parser = undefined,
    };

    var parser: RequestParser = undefined;
    try parser.init(&req);
    defer parser.deinit();
    req.parser = &parser;

    try parseHeaders(&reader, &parser);

    const form_data = try req.formData();
    try std.testing.expectEqualStrings("", form_data.get("foo").?);
}

test "Request.multiFormData: basic key and value" {
    var arena = std.heap.ArenaAllocator.init(std.testing.allocator);
    defer arena.deinit();

    const raw_request = "POST /test HTTP/1.1\r\nContent-Type: multipart/form-data; boundary=--boundary123\r\nContent-Length: 155\r\n\r\n----boundary123\r\nContent-Disposition: form-data; name=\"foo\"\r\n\r\n123\r\n----boundary123\r\nContent-Disposition: form-data; name=\"bar\"\r\n\r\nabc\r\n----boundary123--\r\n";
    var reader = std.Io.Reader.fixed(raw_request);

    var req: Request = .{
        .arena = arena.allocator(),
        .conn = &reader,
        .parser = undefined,
    };

    var parser: RequestParser = undefined;
    try parser.init(&req);
    defer parser.deinit();
    req.parser = &parser;

    try parseHeaders(&reader, &parser);

    const form_data = try req.multiFormData();
    try std.testing.expectEqualStrings("123", form_data.get("foo").?.value);
    try std.testing.expectEqualStrings("abc", form_data.get("bar").?.value);
}

test "Request.multiFormData: entry with filename" {
    var arena = std.heap.ArenaAllocator.init(std.testing.allocator);
    defer arena.deinit();

    const raw_request = "POST /test HTTP/1.1\r\nContent-Type: multipart/form-data; boundary=--boundary123\r\nContent-Length: 133\r\n\r\n----boundary123\r\nContent-Disposition: form-data; name=\"foo\"; filename=\"foo.txt\"\r\nContent-Type: text/plain\r\n\r\n123\r\n----boundary123--\r\n";
    var reader = std.Io.Reader.fixed(raw_request);

    var req: Request = .{
        .arena = arena.allocator(),
        .conn = &reader,
        .parser = undefined,
    };

    var parser: RequestParser = undefined;
    try parser.init(&req);
    defer parser.deinit();
    req.parser = &parser;

    try parseHeaders(&reader, &parser);

    const form_data = try req.multiFormData();
    try std.testing.expectEqualStrings("foo.txt", form_data.get("foo").?.filename.?);
}

test "Request.multiFormData: semicolon in attribute name (regression)" {
    var arena = std.heap.ArenaAllocator.init(std.testing.allocator);
    defer arena.deinit();

    // Content-Disposition attribute name containing ';' — the parser
    // searched for the value-terminating ';' from the field-name start
    // instead of from the value start, so value_end could land before
    // value_start and slice [value_start..value_end] panicked.
    const body = "----b\r\nContent-Disposition: form-data; name;x=v\r\n\r\nval\r\n----b--\r\n";
    var cl_buf: [16]u8 = undefined;
    const cl = try std.fmt.bufPrint(&cl_buf, "{d}", .{body.len});

    var req_buf: std.ArrayListUnmanaged(u8) = .empty;
    defer req_buf.deinit(std.testing.allocator);
    try req_buf.appendSlice(std.testing.allocator, "POST /t HTTP/1.1\r\nContent-Type: multipart/form-data; boundary=--b\r\nContent-Length: ");
    try req_buf.appendSlice(std.testing.allocator, cl);
    try req_buf.appendSlice(std.testing.allocator, "\r\n\r\n");
    try req_buf.appendSlice(std.testing.allocator, body);

    var reader = std.Io.Reader.fixed(req_buf.items);

    var req: Request = .{
        .arena = arena.allocator(),
        .conn = &reader,
        .parser = undefined,
    };

    var parser: RequestParser = undefined;
    try parser.init(&req);
    defer parser.deinit();
    req.parser = &parser;

    try parseHeaders(&reader, &parser);

    // Should error out cleanly, not panic.
    _ = req.multiFormData() catch {};
}

test "Request.multiFormData: trailing backslash in quoted attribute (regression)" {
    var arena = std.heap.ArenaAllocator.init(std.testing.allocator);
    defer arena.deinit();

    // Quoted name value ends with a backslash and no closing quote — the parser
    // currently looks at fields[pos+1] without bounds-checking, panicking on
    // safety-checked builds. Should return InvalidMultiPartEncoding instead.
    const body = "----b\r\nContent-Disposition: form-data; name=\"x\\\r\n\r\nval\r\n----b--\r\n";
    var cl_buf: [16]u8 = undefined;
    const cl = try std.fmt.bufPrint(&cl_buf, "{d}", .{body.len});

    var req_buf: std.ArrayListUnmanaged(u8) = .empty;
    defer req_buf.deinit(std.testing.allocator);
    try req_buf.appendSlice(std.testing.allocator, "POST /t HTTP/1.1\r\nContent-Type: multipart/form-data; boundary=--b\r\nContent-Length: ");
    try req_buf.appendSlice(std.testing.allocator, cl);
    try req_buf.appendSlice(std.testing.allocator, "\r\n\r\n");
    try req_buf.appendSlice(std.testing.allocator, body);

    var reader = std.Io.Reader.fixed(req_buf.items);

    var req: Request = .{
        .arena = arena.allocator(),
        .conn = &reader,
        .parser = undefined,
    };

    var parser: RequestParser = undefined;
    try parser.init(&req);
    defer parser.deinit();
    req.parser = &parser;

    try parseHeaders(&reader, &parser);

    try std.testing.expectError(error.InvalidMultiPartEncoding, req.multiFormData());
}
